#include <stdint.h>

#include "ThingSpeak.h"

#include <WiFi.h>

#include <stdio.h>

#include <stdlib.h>

// define WiFi SSID & PWD for uplink.

#define WLAN_SSID   "Xuan Ba"

#define WLAN_PASS   "h6hsf8g"

#define NUMSAMPLES 5

int curr_samples[NUMSAMPLES];

int volt_samples[NUMSAMPLES];

int temp_samples[NUMSAMPLES];

WiFiClient client;

// resistance at 25 degrees C

#define THERMISTORNOMINAL 10000         

// temp. for nominal resistance (almost always 25 C)

#define TEMPERATURENOMINAL 25   

// The beta coefficient of the thermistor (usually 3000-4000)

#define BCOEFFICIENT 3950

// the value of the 'other' resistor

#define SERIESRESISTOR 10000    

// define Analog for Current and Voltage

const int curr_an_pin = 35;

const int volt_an_pin = 34;

const int ntc_temp_an_pin = 33;

int count = 0;

// thingSpeak Details

#define thingSpeakAddress "xxxxxxxxx"   

#define channelID xxxxx                     

#define writeFeedAPIKey "xxxxxxx"                 

#define readFeedAPIKey "xxxxxxx"                  

#define readFieldAPIKey "xxxxxxxx"                

#define readStatusAPIKey "xxxxxxx"                

void setup() {

  // put your setup code here, to run once:

  // set the serial port at 115200

  Serial.begin(115200);  //Initialize serial

  delay(1000);

  WiFi.mode(WIFI_STA);

  ThingSpeak.begin(client);  // Initialize ThingSpeak

  // todo: create a task to read an pin for get current & voltage and calculate watt and temperature of the solar panel

  xTaskCreate(

                wifi_task,               /* Task function. */

                "wifi_task",             /* String with name of task. */

                1024 * 2,                /* Stack size in bytes. */

                NULL,                    /* Parameter passed as input of the task */

                5,            /* Priority of the task. */

                NULL);                   /* Task handle. */

  Serial.print("Data Reading.");

}

void loop() {

  // put your main code here, to run repeatedly:

  int i=0;

  float solar_curr_adc_val = 0;

  float solar_volt_adc_val = 0;

  for (i = 0; i < NUMSAMPLES; i++) {

                curr_samples[i] = analogRead(curr_an_pin);

                volt_samples[i] = analogRead(volt_an_pin);

                temp_samples[i] = analogRead(ntc_temp_an_pin);

                delay(10);

  }

  // average all the samples out

  float curr_avg = 0;

  float volt_avg = 0;

  float temp_avg = 0;

  for (i = 0; i < NUMSAMPLES; i++) {

                curr_avg += curr_samples[i];

                volt_avg += volt_samples[i];

                temp_avg += temp_samples[i];

  }

  curr_avg /= NUMSAMPLES;

  volt_avg /= NUMSAMPLES;

  temp_avg /= NUMSAMPLES;

  //Serial.print("ADC VALUE = ");

  //Serial.println(ADC_VALUE);

  // convert adc value to voltages for get actual Current & Voltage.

  float solar_curr = (curr_avg * 3.3 ) / (4095);

  float solar_volt = (volt_avg * 3.3 ) / (4095);

  // by using a voltage divider we step down the actual voltage.

  //for that reason we multiply the 6 with avg voltage to get the actual voltage of the solar panel.

  solar_volt *= 6;

  // convert the value to resistance

  temp_avg = 4095 / temp_avg - 1;

  temp_avg = SERIESRESISTOR / temp_avg;

  //Serial.print("Thermistor resistance ");

  //Serial.println(temp_avg);

  float steinhart;

  steinhart = temp_avg / THERMISTORNOMINAL;       // (R/Ro)

  steinhart = log(steinhart);                 // ln(R/Ro)

  steinhart /= BCOEFFICIENT;                           // 1/B * ln(R/Ro)

  steinhart += 1.0 / (TEMPERATURENOMINAL + 273.15); // + (1/To)

  steinhart = 1.0 / steinhart;                 // Invert

  steinhart -= 273.15;                           // convert absolute temp to C

  delay(1000);

  count++;

  Serial.print(".");

  if (count >= 15 ) {

                count = 0;

                Serial.println("=========================================================================");

                Serial.print("Solar Voltage = ");

                Serial.println(solar_volt);

                Serial.print("Solar Current = ");

                Serial.println(solar_curr);

                float solar_watt = solar_volt * solar_curr;

                Serial.print("Solar Watt  = ");

                Serial.println(solar_watt);

                Serial.print("Solar Temperature  = ");

                Serial.println(steinhart);

                Serial.println("=========================================================================");

                if (WiFi.status() == WL_CONNECTED) {

                ThingSpeak.setField(1, solar_volt);

                ThingSpeak.setField(2, solar_curr);

                ThingSpeak.setField(3, solar_watt);

                ThingSpeak.setField(4, steinhart);

                // write to the ThingSpeak channel

                int x = ThingSpeak.writeFields(channelID, writeFeedAPIKey);

                if (x == 200) {

                Serial.println("Channels update successful.");

                }

                else {

                Serial.println("Problem updating channel. HTTP error code " + String(x));

                }

                } else {

                Serial.println("\r\n############################################################");

                Serial.println("Failed to update Data to thingSpeak Server. ");

                Serial.println("WiFi not connected...");

                Serial.println("############################################################\r\n");

                }

  Serial.print("Data Reading.");

  }

}

void wifi_task( void * parameter ) {

  while (1) {

                if (WiFi.status() != WL_CONNECTED) {

                Serial.print("Attempting to connect to SSID: ");

                Serial.println(WLAN_SSID);

                while (WiFi.status() != WL_CONNECTED) {

                WiFi.begin(WLAN_SSID, WLAN_PASS);  // Connect to WPA/WPA2 network. Change this line if using open or WEP network

                Serial.print(".");

                delay(5000);

                }

                Serial.println("\nConnected.");

                Serial.println();

                Serial.println("WiFi connected");

                Serial.println("IP address: ");

                Serial.println(WiFi.localIP());

                }

                vTaskDelay( 1000 / portTICK_PERIOD_MS);

  }

  vTaskDelete(NULL);

}
